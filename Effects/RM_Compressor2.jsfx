desc:RM_Compressor2 [Telemetry]
//author: Tukan
//version 1.2?

slider1:-16<-32,0,0.1>Threshold (dB)
slider2:0<0,100,1>Knee (%)
slider3:4<1,17,0.1>Ratio
slider4:3<1,150,0.1>Attack (ms)
slider5:100<2,1000,1>Release (ms)
slider6:0<-24,24,0.1>Output (dB)
slider7:0<0,1,1{Off,On}>Trick
slider8:0<0,1,1>-Feedback
slider9:0<0,1,1>-Sidechain
slider10:0<0,1,1>-Metermode

slider11:3<0,5,0.01>-Meterspeed
slider12:0<0,1,0.01>-Dry amount

slider13:1<0,6,.01>-Scaling

in_pin:L in
in_pin:R in 
in_pin:Sidechain L
in_pin:Sidechain R
out_pin:L out
out_pin:R out


slider14:0<0,1,0.0001>-Z Telemetry: In Peak
slider15:0<0,1,0.0001>-Z Telemetry: Sidechain Peak
slider16:0<0,1,0.0001>-Z Telemetry: Out Peak
@init
pkIn=0; pkSC=0; pkOut=0;
ext_noinit = 1; 
gfx_ext_retina = 1;

ovrlgain = 1;
last_clicked_item = -1;
threshold = slider1;
knee_width = abs(slider1*2*slider2*0.01);
grold = 1;

inMeter = 0; ginMeter=0; gin2Meter=0;

ratio = slider3;
slope = ((1 - ratio) / ratio);

attack = slider4/1000;
release = slider5/1000;
alphaA = attack>0 ? exp(-1 / (attack * srate)) : 0;
alphaR = release>0 ? exp(-1 / (release * srate)) : 0;

Tlo = (threshold - knee_width / 2);
Thi = (threshold + knee_width / 2);
knee_factor = (slope / (knee_width * 2));

yR = 0;
yA = 0;

function computeGain(x)
(
(x < Tlo) ? x = 0 :
(x > Thi) ? x = slope * (x - threshold) :
(delta = x - Tlo;
x = delta * delta * knee_factor;);
);

function smoothAverage(x, alphaA, alphaR)
(
eps = 0.0000000001;
yR = min(x, alphaR * yR + (1 - alphaR) * x + eps - eps);
yA = alphaA * yA + (1 - alphaA) * yR + eps - eps;
x = yA;
);

dbc = 20/log(10);
function db2ratio(d) ( 10^(d/20); );
function ratio2db(r) ( log(abs(r))*dbc; );

gr_meter = 1;
gr_meter_decay = exp(1/(1*srate));














meter_dif = 0.0005;

function meterspeed()
(
mspd = slider11;
meter_speed = mspd/srate;

mspd == 5 ? (
gr_meter2 < gr_meter ? (
  meter_speed /= 5;
  abs(gr_meter2 - gr_meter) > meter_dif ? (
    gr_meter2 = gr_meter2 + (meter_speed);
    
  );
//gr_meter *= 1.1;

):(


    gr_meter2 = gr_meter;

  );
);
;

mspd == 0 ? (
  gr_meter2 > gr_meter ? (
    gr_meter2 = gr_meter;
  );
);

mspd > 0 ? (
mspd < 5 ? (
gr_meter2 < gr_meter ? (

  abs(gr_meter2 - gr_meter) > meter_dif ? (
    gr_meter2 = gr_meter2 + (meter_speed);
    
  );
//gr_meter *= 1.1;

):(

  abs(gr_meter2 - gr_meter) > meter_dif ? (
    gr_meter2 = gr_meter2 - meter_speed*8;
   gr_meter2 < gr_meter ? gr_meter2 = gr_meter;
  );
);
);
);

gr_meter2 > 1 ? gr_meter2 =1;
);










  gfx_clear=0;
  lastCap=1;
  mouse_wheel=1;
  coords=srate+1;
  
    function limitgui(s low up)
                  ( min(max(s, low), up) );
    function deg(r)
                  ( r / $pi * 180 );
    function rad(d)
                  ( d * $pi / 180 );
                  
                  
 /******************************* GUI Function ***********************************/
 
 function xy(x y)
            ( gfx_x = x; gfx_y = y; );
 
 function rgb(r g b)
             ( gfx_r = r; gfx_g = g; gfx_b = b; );
 
 function rectIn  (xIn_ yIn_ wIn hIn)
   instance       (xIn yIn widthIn heightIn)
                  (
                   xIn = xIn_;
                   yIn = yIn_;
                   widthIn = wIn;
                   heightIn = hIn;
                   );
                 
 function rectOut  (xOut_ yOut_ wOut hOut c i)
   instance        (xOut yOut widthOut heightOut count index)
                   (
                    xOut = xOut_;
                    yOut = yOut_;
                    widthOut = wOut;
                    heightOut = hOut;
                    count = c;
                    index = i;
                    );                
 
 function potVal(v)
   instance      (frame val minVal maxVal count heightOut)
                 (
                  val = v;
                  frame = (val - minVal) * (count - 1) / (maxVal - minVal) + 0.5;
                  frame |= 0;
                 );
 
 function potCfg  (default_ minVal_ maxVal_ step_)
   instance        (default minVal maxVal step)
                   (
                    default = default_;
                    minVal = minVal_;
                    maxVal = maxVal_;
                    step    = step_;
                    this.potVal(default);
                   );
 
 function bkg(wImg hImg indImg)
              (
                gfx_a=1;
                coords[0] = coords[4] = 0;
                coords[1] = coords[5] = 0;;
                coords[2] = coords[6] = wImg;
                coords[3] = coords[7] = hImg;
                gfx_blitext(indImg, coords, 0);
               ); 
 
 function potDrawIn ()
   instance        (xIn yIn widthIn heightIn val)
                   (
                      gfx_r= 1; gfx_g = gfx_b =0;gfx_a= 0.5;
                      
                      gfx_line(xIn,yIn,xIn,heightIn+yIn);
                      gfx_line(xIn,heightIn+yIn,xIn+widthIn,heightIn+yIn);
                      gfx_line(xIn+widthIn,yIn,xIn+widthIn,heightIn+yIn);
                      gfx_line(xIn,yIn,xIn+widthIn,yIn);
       
                      gfx_r = gfx_g = gfx_b = 0.75;
                      gfx_x = xIn + widthIn/2-12;
                      gfx_y = yIn+heightIn/2-5;
                      gfx_drawnumber(val, 1);                   
                   );
 
 function knob()
   instance   (xOut yOut widthOut heightOut index frame val)
              (
               
                gfx_a=1;
                coords[0] = 0;
                coords[1] = frame * heightOut;
                coords[2] = coords[6] = widthOut;
                coords[3] = coords[7] = heightOut;
                coords[4] = xOut;
                coords[5] = yOut;
                gfx_blitext(index, coords, 0);
               );                                     
 
 function collision  ()
   instance          (xIn yIn widthIn heightIn)
                     (
                      mouse_x > xIn*zfct && mouse_x < xIn*zfct + widthIn*zfct && mouse_y > yIn*zfct && mouse_y < yIn*zfct + heightIn*zfct
                     );
 
 function dragStart ()
   instance          (dragging yOld default)
                     (
                      !ctrl ? (
                      yOld = mouse_y;
                      dragging = 1;
                     ) : 
                      this.potVal(default);
                     );
 
 function dragStop()
   instance        (dragging val valOld)
                   (
                    dragging = 0;
                    valOld = val;
                   );
 
    function potDrag()
          local      (val)
            instance   (valOld minVal maxVal yOld step)
                     (
                       !shift ? (
                       mstep = (this.maxval - this.minval)/100;
                      val = valOld + (yOld - mouse_y) * mstep;
                      val = limitgui(val, minVal, maxVal);
                      this.potVal(val);
                      ):(
                      val = valOld + (yOld - mouse_y) * step;
                      val = limitgui(val, minVal, maxVal);
                      this.potVal(val);
                      );
                     );
                     
    function potToggle()
          local      (val)
            instance   (valOld minVal maxVal yOld step)
                     (
                      valold == 0 ? (val = 1):(val=0);
                      val = limitgui(val, minVal, maxVal);
                      this.potVal(val);
                      valOld=val;
                       );
    
    function potWheel()
        local         (val)
        instance      (valOld minVal maxVal step)
                      (
                      mstep = (this.maxval - this.minval)/100;
                       val = valOld + (mouse_wheel/36)*mstep;
                       val = limitgui(val, minVal, maxVal);
                       this.potVal(val);
                       valOld=val;
                       mouse_wheel=0;
                        );
 
   
 gthresh.potcfg  (-16,-32,-0,.032); //default. von, bis, step
 gratio.potcfg (4,1,15,.015);
 gatt.potcfg   (20,0,150,1);
 grel.potcfg   (200,2,1000,1);
 goutgain.potcfg   (0,-24,24,.048);
 gfeedback.potcfg   (0,0,1,1);
 Trick.potcfg   (0,0,1,1);
 SC.potcfg   (0,0,1,1);
 screw.potcfg   (3,0,5,.01);
 gknee.potcfg   (0,0,100,.1);
 gdry.potcfg   (0,0,1,.001);
 
 knobsx = 50; 
 knobsy = 50;
 gthresh.rectIn(knobsx,knobsy,75,75);//xpos,ypos,weite,höhe des img
 gthresh.rectOut(knobsx,knobsy, 75, 75, 61, 1); //Xpos,ypos,weite,höhe,Frames,bitmap

 gknee.rectIn(knobsx+95,knobsy+75, 10, 10);
 gknee.rectOut(knobsx+95,knobsy+75, 10, 10, 61, 6);
 
 gratio.rectIn(knobsx+120,knobsy, 75, 75);
 gratio.rectOut(knobsx+120,knobsy, 75, 75, 61, 2);
 
 gatt.rectIn(knobsx+7,knobsy+140, 60, 60); 
 gatt.rectOut(knobsx+7,knobsy+140, 60, 60, 61, 3);
 
grel.rectIn(knobsx+127,knobsy+140, 60, 60); 
grel.rectOut(knobsx+127,knobsy+140, 60, 60, 61, 3);

knobsx+=260; knobsy+=133;
goutgain.rectIn(knobsx,knobsy, 75, 75); 
goutgain.rectOut(knobsx,knobsy, 75, 75, 61, 2);

gdry.rectIn(knobsx-18,knobsy+85, 10, 10); 
gdry.rectOut(knobsx-18,knobsy+85, 10, 10, 61, 6);

screw.rectIn(knobsx+324,knobsy-175, 10, 10); 
screw.rectOut(knobsx+324,knobsy-175, 10, 10, 61, 6);


knobsx+=120;
gfeedback.rectIn(knobsx,knobsy, 48, 60); 
gfeedback.rectOut(knobsx,knobsy, 48, 60, 2, 4);

Trick.rectIn(knobsx+60,knobsy, 48, 60); 
Trick.rectOut(knobsx+60,knobsy, 48, 60, 2, 4);

SC.rectIn(knobsx+120,knobsy, 48, 60); 
SC.rectOut(knobsx+120,knobsy, 48, 60, 2, 4);









  
  errcnt = 0;
  tot_nbr_spl = 0;
  scnt = 0;
  
  offset = 0.0074;//0.0074;
  
  nd_posL = nd_posR = 0.6800809;;
  nd_speedL = nd_speedR = 0;
  
  dt = 10 / srate;
  
  mom = 0.00042;
  
  dbL = dbR = 0;
  overL = overR = 0;
  
  fact_up = 10 ^ (( 0 - 10)/20) * 0.3785 ;
  wl   = 0;
  damp = 0.995;//935; //1 - slider5 * (48000 / srate);
  
  mnmode = 0; // (0 ST, 1 SumMono, 2 MaxMono)
  meterInL = spl0;
  meterInR = spl1;
  
  function mnMetersample(meterInL, meterInR, mnmode, mnmom) //Mom = speed
  (
  tot_nbr_spl += 1;
  
  smpL = meterInL; 
  smpR = meterInR;
  
  mnmode == 1 ? ( 
    smpL = (meterInL + meterInR) * 0.5;
    smpR = smpL;  
  );
  
  mnmode >= 2 ? ( 
    smpL = max(meterInL, meterInR);
    smpR = smpL; 
  );
  
  smpL = abs(smpL);
  smpR = abs(smpR);
  
   
  scnt += 1;
  
  scnt === 10 ? (
      
    // move left needle
    
      force = smpL * fact_up  -  (nd_posL * .1 + offset);
      
      nd_speedL += force * dt / mnmom;  
      nd_speedL = nd_speedL * damp;
      nd_posL += nd_speedL * dt;
      nd_posL < 0 || nd_posL > 1 ? nd_speedL = 0;
    
      nd_posL = min(max(nd_posL,0),1);
    
      // move right needle
  
      force = smpR * fact_up  - (nd_posR * .1 + offset);
      
      nd_speedR += force * dt /mnmom;  
      nd_speedR = nd_speedR * damp;
      nd_posR += nd_speedR * dt;
      nd_posR < 0 || nd_posR > 1 ? nd_speedR = 0;
    
      nd_posR = min(max(nd_posR,0),1);
       
      overL -= 10;
      overR -= 10;
  
      scnt = 0;
    
  );
  );
  
  function mnmetergfx(mnmeterscale, mnmeterx, mnmetery, twometer, mnch) //(0=flexible, x,y,redneedle, channel 1or2)
  (
  tot_nbr_spl_g  = tot_nbr_spl;
  
  overL_g = overL;
  overR_g = overR;
  nd_posL_g = nd_posL;
  nd_posR_g = nd_posR;
  
  tot_nbr_spl_g === tot_nbr_spl ? (
  
    dbL = (nd_posL_g * 23) - 20;
    dbR = (nd_posR_g * 23) - 20;
    
  ) : (
    errcnt += 1; // thread collision
  );
  
  
  gfx_a = 1; gfx_x = mnmeterx; gfx_y=mnmetery;
  
  mnmeterscale == 0 ? (mnblitscale = gfx_w/190):(mnblitscale = mnmeterscale);
  gfx_blit(7,mnblitscale,0);
  
  
  mnw1 = $pi * 16.5 / 180; 
  mnw2 = $pi * 45 / 180;
  
  mnxw = 190*mnblitscale; //max(1,floor((gfx_w-30) / 2));
  mnyw = 92*mnblitscale;//floor(mnxw / 1.5);
  
  mnr1 = 130*mnblitscale;//floor(mnyw * 0.85);
  
  
  
   mnxd = 10 + chan*(mnxw+10);
    mode === 1 ? mnxd += floor(mnxw/2);
    
    mnyd = 10;
  
    mnxa = mnblitscale*190/2;//gfx_w/2;//floor(mnxd + mnxw / 2);
    mnya = floor(mnyd + mnyw * 1.1);
  
  
  twometer ? (
    chan = 1;
    chan == 0 ? (mnph = dbL; gfx_r=gfx_b=gfx_g=0) : (mnph = dbR; gfx_r=1;gfx_b=gfx_g=0);
  
    mnph = 51 + (mnph+20)/23*83;//45 + (mnph+20)/23*90; 
    aay = abs((90 - abs(mnph))); 
    mnr1 = mnblitscale*(80+aay/5);
    
    mnph = mnph * ($pi / 180);
      
    cosp = cos(mnph);
    sinp = sin(mnph);
      
      mnya = mnmetery+110*mnblitscale;
      mnxa= mnmeterx+2+(190*mnblitscale/2);//-(gfx_w/50);
    mnx1 = mnxa - cosp * mnr1 * 0.25;
    mny1 = mnya - sinp * mnr1 * 0.25;
  
    mnx2 = mnxa - cosp * mnr1 * 1.1;
    mny2 = mnya - sinp * mnr1 * 1.1;
  
    gfx_x = mnx1;
    gfx_y = mny1; 
    gfx_lineto(mnx2, mny2);
   );
    
  chan = 0;
  
    chan == 0 ? (mnph = dbL; gfx_r=gfx_b=gfx_g=0) : (mnph = dbR; gfx_r=1;gfx_b=gfx_g=0);
    mnch == 2 ? (mnph= dbR);
    mnph = 51 + (mnph+20)/23*83;//45 + (mnph+20)/23*90; 
    aay = abs((90 - abs(mnph))); 
    mnr1 = mnblitscale*(80+aay/5);
    
    mnph = mnph * ($pi / 180);
      
    cosp = cos(mnph);
    sinp = sin(mnph);
      
      mnya = mnmetery+110*mnblitscale;
      mnxa= mnmeterx+2+(190*mnblitscale/2);//-(gfx_w/50);
    mnx1 = mnxa - cosp * mnr1 * 0.25;
    mny1 = mnya - sinp * mnr1 * 0.25;
  
    mnx2 = mnxa - cosp * mnr1 * 1.1;
    mny2 = mnya - sinp * mnr1 * 1.1;
  
    gfx_x = mnx1;
    gfx_y = mny1; 
    gfx_lineto(mnx2, mny2);
    
    );





@slider
threshold = slider1;
knee_width = abs(slider1*2*slider2*0.01);

ratio = slider3;
slope = ((1 - ratio) / ratio);

attack = slider4/1000;
release = slider5/1000;
alphaA = attack>0 ? exp(-1 / (attack * srate)) : 0;
alphaR = release>0 ? exp(-1 / (release * srate)) : 0;

Tlo = (threshold - knee_width / 2);
Thi = (threshold + knee_width / 2);
knee_factor = (slope / (knee_width * 2));

output = 10^(slider6/20);

listen = slider7;

@sample
// telemetry peak capture
pkIn = max(pkIn, max(abs(spl0), abs(spl1)));
pkSC = max(pkSC, max(abs(spl2), abs(spl3)));
/*
inL ? (oops = 0):(inL = spl0);
inR ? (oops = 0):(inR = spl1);
*/
clean0 = spl0;
clean1 = spl1;

slider8 == 1 ?(
inL = feedL;//spl0;
inR = feedR;// spl1;
):(
inL = spl0; inR = spl1;
);
slider9 == 1 ? (
inL = spl2; inR = spl3;
);

inMeter = max(abs(spl0),abs(spl1));

xL = max(abs(inL),abs(inR));

xG = ratio2db(xL);
yG = computeGain(xG);
cG = smoothAverage(yG, alphaA, alphaR);
cL = db2ratio(cG);

feedL = spl0*cL; feedR=spl1*cL;

listen == 0 ? (
spl0 = clean0 * slider12 + spl0 * cL * output;
spl1 = clean1 * slider12 + spl1 * cL * output;
);
listen == 1 ? (
spl0 = clean0 * slider12 + ((spl0 * cL) - spl0)  * -output;
spl1 = clean1 * slider12 + ((spl1 * cL) - spl1)  * -output;
);
// telemetry: output peak
pkOut = max(pkOut, max(abs(spl0), abs(spl1)));


gr = db2ratio(cL);
grv = ratio2db(gr);
//abs(gr_meter - meter_old) > 0.01 ? (gr_meter_decay = 1.01;//.111111;
gr_meter_decay = 1.1;
grv < gr_meter ? gr_meter=grv : ( gr_meter = grv; gr_meter>1?gr_meter=1; );// gr_meter*=gr_meter_decay
grmin = min( min(cG,cG) , grmin);

meterspeed();
meter_old = gr_meter;


  mom = 0.0001 + 0.00032 * abs(100-slider11*20)^3 / 125000;
  mnmetersample(grv*0.63,grv*0.63,1,mom); // mode = (0 ST, 1 SumMono, 2 MaxMono), mnmom = speed
 
 
// inMeter = db2
inMeter > ginMeter ? ginMeter=inMeter : (ginMeter*=0.99997;  ginMeter<0.1?ginMeter=0; );








/// LIMITER ///////////////
ovrlParam1=1.5; //ovrlthresh
ovrlParam2=0.5; //output
ovrlParam3=0.5+0*0.5; //ovrlattack
ovrlParam4=0.5+0*0.5; //ovrlrelease
ovrlParam5=0; //softknee

//hard knee
ovrlthresh = pow(10, (2 * ovrlParam1) - 2);

ovrltrim = pow(10, (2 * ovrlParam2) - 1);
ovrlatt = pow(10, -0.01 - (2 * ovrlParam3));
ovrlrel = pow(10, -2 - (3 * ovrlParam4));


ovrlth = ovrlthresh;
ovrlg = ovrlgain;
ovrlat = ovrlatt;
ovrlre = ovrlrel;
ovrltr = ovrltrim;

ovrlol = spl0;
ovrlor = spl1;

ovrllev = (0.5 * ovrlg * abs(ovrlol + ovrlor));

(ovrllev > ovrlth) ?
(
ovrlg = ovrlg - (ovrlat * (ovrllev - ovrlth));
)
: //below ovrlthreshold
(
ovrlg = ovrlg + (ovrlre * (1 - ovrlg));
);

spl0 = (ovrlol * ovrltr * ovrlg);
spl1 = (ovrlor * ovrltr * ovrlg);

ovrlgain = ovrlg;

 

@block
slider14 = pkIn;
slider15 = pkSC;
slider16 = pkOut;
pkIn = 0; pkSC = 0; pkOut = 0;
slider_automate(slider14);
slider_automate(slider15);
slider_automate(slider16);
